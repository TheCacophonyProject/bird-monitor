apply plugin: 'com.android.application'

ext.versionMajor = 1
ext.versionMinor = 6
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = false
// Increased to 15 ie Android 4.03 Icecream sandwich  to allow debugging with instant run https://developer.android.com/studio/run/
ext.minimumSdkVersion = 15

repositories {
    //  mavenCentral()
    jcenter()
}


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "nz.org.cacophony.birdmonitor"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 28
        versionCode generateVersionCode() 
        versionName generateVersionName()

        archivesBaseName = "Bird Monitor-$versionName"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.dev'
            debuggable true
        }

    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        animationsDisabled = true
    }
    buildToolsVersion '28.0.3'
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName;
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.loopj.android:android-async-http:1.4.9'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    // https://stackoverflow.com/questions/26633349/disable-ssl-as-a-protocol-in-httpsurlconnection
    implementation 'info.guardianproject.netcipher:netcipher:1.2'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.squareup.okio:okio:2.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.android.support:support-v4:28.0.0'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    //https://stackoverflow.com/questions/45541666/espresso-test-failing-no-interface-method-trackusage-in-usagetracker-java
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    // Optional -- UI testing with UI Automator
    //  androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0-rc02'
}
