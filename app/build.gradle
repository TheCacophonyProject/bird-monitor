apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Increased to 15 ie Android 4.03 Icecream sandwich  to allow debugging with instant run https://developer.android.com/studio/run/
// Increated to 16 for firebase
ext.minimumSdkVersion = 16

repositories {
    //  mavenCentral()
    jcenter()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
def performSigning = keystorePropertiesFile.exists()
if (performSigning) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 28
    if (performSigning) {
        signingConfigs{
            config {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    defaultConfig {
        applicationId "nz.org.cacophony.birdmonitor"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 28
        versionCode 150010700
        //minimumSdkVersion * 10000000 + versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "1.6.0"

        archivesBaseName = "Bird Monitor-$versionName"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (performSigning) {
                signingConfig signingConfigs.config
            }
        }
        debug {
            debuggable true
            if (performSigning) {
                signingConfig signingConfigs.config
            }
        }

    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        animationsDisabled = true
    }
    buildToolsVersion '28.0.3'
}

dependencies {

    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.loopj.android:android-async-http:1.4.9'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    // https://stackoverflow.com/questions/26633349/disable-ssl-as-a-protocol-in-httpsurlconnection
    implementation 'info.guardianproject.netcipher:netcipher:1.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.squareup.okio:okio:2.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // Optional -- UI testing with Espresso
    //https://stackoverflow.com/questions/45541666/espresso-test-failing-no-interface-method-trackusage-in-usagetracker-java
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    // Optional -- UI testing with UI Automator
    //  androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    implementation 'androidx.navigation:navigation-fragment:2.2.0-alpha01'

    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

}
apply plugin: 'com.google.gms.google-services'  // Google Play services Gradle plugin
