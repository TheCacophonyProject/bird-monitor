apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// Increased to 15 ie Android 4.03 Icecream sandwich  to allow debugging with instant run https://developer.android.com/studio/run/
// Increated to 16 for firebase
ext.minimumSdkVersion = 19
ext.versionMajor = 1
ext.versionMinor = 10
ext.versionPatch = 5
ext.versionClassifier = null
ext.isSnapshot = false

repositories {
    //  mavenCentral()
    jcenter()
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
def performSigning = keystorePropertiesFile.exists()
if (performSigning) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 32
    if (performSigning) {
        signingConfigs {
            config {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    defaultConfig {
        applicationId "nz.org.cacophony.birdmonitor"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 32
        versionCode generateVersionCode()
        versionName generateVersionName()
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = "Bird-Monitor-$versionName"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (performSigning) {
                signingConfig signingConfigs.config
            }
        }
        debug {
            debuggable true
            if (performSigning) {
                signingConfig signingConfigs.config
            }
        }

    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    testOptions {
        animationsDisabled = true
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName;
}

dependencies {

    implementation 'com.google.firebase:firebase-analytics:21.1.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.loopj.android:android-async-http:1.4.11'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    // https://stackoverflow.com/questions/26633349/disable-ssl-as-a-protocol-in-httpsurlconnection
    implementation 'info.guardianproject.netcipher:netcipher:1.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.squareup.okio:okio:3.2.0'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.10'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation 'androidx.annotation:annotation:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    // Optional -- UI testing with Espresso
    //https://stackoverflow.com/questions/45541666/espresso-test-failing-no-interface-method-trackusage-in-usagetracker-java
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    // Optional -- UI testing with UI Automator
    //  androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    implementation 'androidx.navigation:navigation-fragment:2.5.0'

    implementation 'com.google.firebase:firebase-core:21.1.0'
    implementation 'com.google.firebase:firebase-analytics:21.1.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.11'
    implementation 'com.google.firebase:firebase-analytics:21.1.0'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

}
apply plugin: 'com.google.gms.google-services'  // Google Play services Gradle plugin
